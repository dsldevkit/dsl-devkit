/*******************************************************************************
 * Copyright (c) 2016 Avaloq Evolution AG and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     Avaloq Evolution AG - initial API and implementation
 *******************************************************************************/
module Format

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.generator.*
import org.eclipse.xtext.ui.generator.*
import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

import com.avaloq.tools.ddk.xtext.generator.*

var file.extensions = "format"
var projectName = "com.avaloq.tools.ddk.xtext.format"
var rootPath = ".."
var runtimeProject = "${rootPath}/${projectName}"

Workflow {
    bean = StandaloneSetup {
        scanClassPath = true
        platformUri = "${runtimeProject}/.."
        registerGeneratedEPackage = "org.eclipse.xtext.xbase.XbasePackage"
        registerGenModelFile = "platform:/resource/org.eclipse.xtext.xbase/model/Xbase.genmodel"
        registerGenModelFile = "platform:/resource/org.eclipse.xtext/org/eclipse/xtext/Xtext.genmodel"
        registerGenModelFile = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.genmodel"
    }

  component = XtextGenerator {
    configuration = GeneratorModule {
      project = model.project.ProjectConfig {
        baseName = projectName
        rootPath = rootPath
        createEclipseMetaData = true
        forceDisableIdeProject = false
        runtimeTest = {
          enabled = true
        }
        eclipsePlugin = {
          enabled = true
        }
        eclipsePluginTest = {
          enabled = true
        }
        genericIde = {
          enabled = true
        }
      }
      code = {
        encoding = "UTF-8"
        lineDelimiter = "\r\n"
        fileHeader = "/*\n * generated by Xtext \${version}\n */"
      }
    }
    language = StandardLanguage {
      name = "${projectName}.Format"
      fileExtensions = file.extensions

      contentAssist = {
        generateXtendStub = false
      }
      formatter = {
        generateStub = false
      }
      labelProvider = {
        generateXtendStub = false
      }
      outline = {
        generateXtendStub = false
      }
      quickFixProvider = {
        generateXtendStub = false
      }
      serializer = {
        generateStub = true
        generateXtendStub = false
      }
      validator = {
        // composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
      }

      fragment = formatting.FormatterFragment2 {
        generateStub = true
        generateXtendStub = false
      }

      // Strip bundle-version introduced e.g. by SerializerFragment2
      fragment = BundleVersionStripperFragment {
        bundle = "org.eclipse.xtext.xbase.lib"
        bundle = "org.eclipse.xtend.lib"
        bundle = "org.antlr.runtime"
        bundle = "org.objectweb.asm"
      }
    }
  }

}
