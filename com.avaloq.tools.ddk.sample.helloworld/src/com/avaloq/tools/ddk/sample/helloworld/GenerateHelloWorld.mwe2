module com.avaloq.tools.ddk.sample.helloworld.GenerateHelloWorld

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import com.avaloq.tools.ddk.xtext.generator.*
import com.avaloq.tools.ddk.xtext.generator.model.project.*

var rootPath = ".."
var baseName = "com.avaloq.tools.ddk.sample.helloworld"

Workflow {

  component = XtextGenerator {
    configuration = GeneratorModule {
      project = ProjectConfig {
        baseName = baseName
        rootPath = rootPath
        runtimeTest = {
          enabled = false
        }
        forceDisableIdeProject = false
        eclipsePlugin = {
          enabled = true
        }
        eclipsePluginTest = {
          enabled = true
        }
        createEclipseMetaData = true
      }
      code = {
        encoding = "UTF-8"
        lineDelimiter = "\r\n"
        fileHeader = "/*\n * generated by Xtext\n */"
        preferXtendStubs = false
      }
    }
    language = StandardLanguage {
      name = "com.avaloq.tools.ddk.sample.helloworld.HelloWorld"
      fileExtensions = "helloworld"
      parserGenerator = parser.antlr.AnnotationAwareXtextAntlrGeneratorFragment2 {

      }

      contentAssist = ui.contentAssist.AnnotationAwareContentAssistFragment2 {

      }

      serializer = {
        generateStub = false
      }

      generator = {
        generateStub = false
      }

      scopeProvider = {
        generateStub = false
      }

      // Strip bundle-version introduced e.g. by SerializerFragment2
      fragment = BundleVersionStripperFragment {
        bundle = "org.eclipse.xtext.xbase.lib"
        bundle = "org.antlr.runtime"
      }
    }
  }
}
